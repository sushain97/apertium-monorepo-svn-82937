Alphabet

 А Б В Г Д Е Ё Ж З И Й К Л М Н О П Р С Т У Ү Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я  
 а б в г д е ё ж з и й к л м н о п р с т у ү ф х ц ч ш щ ъ ы ь э ю я 

 %, %.

!Suffix final variant vowel for hard vs soft
!Ь2:ь !
Ь2:0 !

! derivational suffix archivowel with "o" and "a" 
!realisation  in deverbal noun ending
А1:а !

! conjugational suffix archivowels 
!showing "a" vs "я" and "ы" vs "и" alternation
!А2:а !А2
А2:я !А2
Ы2:и !
Ы2:ы !

! derivational suffix archivowel  "mo" and "me"
О1:е !
!О1:э !
!О1:о !
!О1:0 !

! derivation suffix-initial archivowel 
%^О2:е
%^О2:э
%^О2:о
%^О2:0

Е3:е ! тне тнэ

! clitic archiconsonant
Г1:г !
Г1:к !
Г1:0 !

! Prolative Ga
Г2:г !
Г2:к !
Г2:в !

! imperative suffix
К1:к !

! connegative suffix rendering vowel
! панемс пань>%^О2К3 : пан00е0
К3:0 !

! Diminutive in ке and не нэ Н1е
Н1:к
Н1:н

%^SLoss:0

%- !Hyphen with constructions like "-buti"


 %>:0 ;

Sets

Vows = а о у ы э я ё ю и е 
       А О У Ы Э Я Ё Ю И Е ;
verbStemVows = а о э я ё е 
               А О Э Я Ё Е ;
verbStemVowStrong = а о э я ё е 
                    А О Э Я Ё Е ;
noGlideVow = а о у ы э 
             А О У Ы Э ;
glideVow = я ё ю и е 
           Я Ё Ю И Е ;
BTV = а о у я ё ю 
      А О У Я Ё Ю ; ! Back Trigger Vowels
FTV = ы и е э 
      Ы И Е Э ; ! Front Trigger Vowels

Cns = б в г д ж з й к л м н п р с т ф х ц ч ш щ 
      Б В Г Д Ж З Й К Л М Н П Р С Т Ф Х Ц Ч Ш Щ ;
NPC = б в г д ж з к л м н п р с т ф х ц ч ш щ 
      Б В Г Д Ж З К Л М Н П Р С Т Ф Х Ц Ч Ш Щ ; ! Nonpalatal Consonants
CnsVoiced = б в г д ж з й л м н р 
            Б В Г Д Ж З Й Л М Н Р ;
CnsVoicedDent = д з л н р 
                Д З Л Н Р ;
CnsVoicedNonDent = б в г ж й м 
                   Б В Г Ж Й М ;
CnsVoiceless = к п с т ф х ц ч ш щ 
               К П С Т Ф Х Ц Ч Ш Щ ;
CnsVoicelessDent = с т ц 
                   С Т Ц ;
CnsVoicelessNonDent = к п ф х ч ш щ 
                      К П Ф Х Ч Ш Щ ;
CnsNonDent = б в г ж к м п ф х ч ш щ 
             Б В Г Ж К М П Ф Х Ч Ш Щ ;
CnsDent = д з л н р с т ц 
          Д З Л Н Р С Т Ц  ;
CnsDentNoL = д з н р с т ц 
             Д З Н Р С Т Ц  ;

Letters = Vows Cns ь ъ Ь Ъ ;

Definitions

! Front Trigger Consonants (less ль)
FTC = [ й: | д ь: | з ь: | н ь: | р ь: | с ь: | т ь: | ц ь: ] ;

! All Consonants
AC = [ FTC | л ь: | NPC ] ;

! Left Front Context Soft
LFCS = [ :FTV AC* CnsNonDent | :FTV AC* FTC |  BTV FTC AC* CnsNonDent  | FTC | л ь: ] ;

! Left Front Context Hard
LFCH = [ :FTV AC* CnsDent | BTV FTC  AC* CnsDent+ | [ BTV л ь [ к с ] ] ] ;

! Front Trigger
FT = [ LFCS | LFCH | л ь: | :FTV ] ; 
!! This should be followed by a morpheme boundary marker %>

LFCHMore =  [ :FTV | FTC | л ь: ]  %>: [ %^О2: н з | с ( т ) ] ;

! Left Back Context
LBC =    :BTV ( %>: %^О2:0 ) NPC* ( %>: )  :NPC+   ;
!BTV л ь [ ф | т | д ] ] 

NoFront = [ %>:  | :BTV | BTV: | :NPC | О1: | %^О2: ]* ;

!LBCCns = LBC \[ #: | FT | :FTV | FTC | л ь: | ь: | й: ]* :NPC+ ;

PossX = [ м | н Ь2: | т Ь2: | н т Ь2: | ( н ) з О1: | н О1: к | м О1: к | н к | ( н ) с т | с к ] ;

CaseX = [ с ь: | [ н | с ] т ь | [ н | с ] т е н ь | н е н ь | д: О1: | с ( т ) О1: | з | с | в | к с | Г2: а | в: т О1: м О1: |  ш к а | н е: | н е: к | н ь: ] ;

CasePossX = [ т е [ т | н ] ь | н с т [ О1: | э н ь ] | з %>: %^О2: PossX ] ;

InfX = [ м с | м О1: | м А1: | м д О1: | м г а | м с О1: | м с т О1: | з ь: ] ;

ConjX = [ ( з ) м а к | ( з ) м а м | ( з ) м и з ь | н з а т | с с О1: | з О1: | с т | з т | К1: Ь2: | К3: | д О1: | (в: ( О1: ) ) л ь: ( т ь | с е ) | (в: ( О1: ) ) л и [[ н | т | т ( и | е ) н ] ь | д е | н [ е | и ] к | н к | з ь: | к | я | ( н ) з е | н з [ е | и ] т ь | [ м | д ] [ и | е ] з ь | м [ и | е ] [ м | к ]  ] | т с ( т ) ь   ] ;

Back =  :* [ :BTV \[ :FTV | :й | :ь  ]* ( л ь ) [NPC]]  ;

! Word-final elements
WFE = [ [ PossX | CaseX | CasePossX  | ConjX | т н е: ] [  #:  | %>: [  Г1: | :Cns ] |  %- ]  | InfX |  #: | %>: [  Г1: | :Cns ] |  %- ] ;

SIVV = [ PossX | InfX | в | н е н ь | т е н ь | т е т ь | н с т э н ь | н с т О1: | н т е н ь | с т е н ь | л ь | л и | к с | ш к а | с | з  %>:  %^О2: | т ь: | н т ь: | н ь: | в:  О1:  л | т %> с ь | т %> с т ь | К3: | д О1: | с ь ] ;
!! Suffix-Initial Variable Vowel



Rules

!Changes in the stem vowels before "10", "20", "30" and "60"
"verbStemVowStrong:0"
 X:Y <=> [ Cns | Vows ] _  %>: [А2:я|А2:а [ н | т ]| Ы2:] ;
!                   Cns  _ К4:0 %>:  Ы2: ;
          where X in (а о э ы у я ё е и ю)
                Y in (0 0 0 0 0 0 0 0 0 0) matched ;

! Deverbal noun in ОмА
"A1:o"
А1:о <=>  [ а |я ] %>: %^О2:0  м  _ ;

"Y2:yi"
Ы2:и <=> [ я:0 | ё:0 | е:0 | ю:0 | ь:0 | й:0 | CnsNonDent (noGlideVow:0)]  %>: _ ;


"O1:e"
О1:э <=>   :FTV %>: [ с ( т ) | %^О2:0 ( н )  з | %^О2:0 м [ с | с т ] ] _ [  #: |  %- | %>: [ Г1: | %^О2:0 | :Cns ] ]  ;
          LFCH %>: [ с ( т ) | [ %^О2:э |  %^О2:0 ] з | %^О2:э н   з | %^О2:э м [ с | с т ] ] _ [  #: |  %- | %>: [ Г1: | %^О2:0 | :Cns ] ]  ;
          LFCS  %>: [ с ( т ) | %^О2:е  н  з |  [ %^О2:е | %^О2:0 ]  з | [ %^О2:е | %^О2:0 ]  м [ с | с т ] ] _ [  #: |  %- | %>: [ Г1: | %^О2:0 | :Cns ] ]  ;
          LFCH %>: ( д: ) _ [  #: |  %- | %>: [ Г1: | :Cns | %^О2:0 ] ]  ;
          LFCH %>: в: т _ м О1:  ;
          LFCH %>: н _ к  ;
! LFCHMore _ [  #: |  %- | %>: [ Г1: | %^О2:0 | :Cns ] ]  ;
!          FT %>: [ %^О2: ( н ) з | с ( т ) ] _ [ # |  %>: [ :Cns | Г1: ]] ;
!2013-04-16!  [ LFCH | [[ LFCH | LFCS ] %>: | :FTV ( %>: ) ] з  ] %>: _ [ ( %>: ) WFE | %>: %^О2:0 | %>: :Cns ]; !! move to O2:e

!!
"O1:o"
О1:о <=> LBC _ [  #: | %- | %>: [ Г1: | :Cns | %^О2:0 ] ] ;
        [ LBC  %>: | :BTV ( %>: ) ] [ д: | с ( т ) | [ %^О2:о | %^О2:0 ] ( н )  з | [ %^О2:о | %^О2:0 ]  м ( д | с | с т ) | в: т О1:о м ] _ [  #: | %- | %>: [ Г1: | :Cns | %^О2:0 ] ] ;
         [ LBC  %>: | :BTV ( %>: ) ] в: т _ м О1: ;

        [ LBC  %>: | :BTV ( %>: ) ] [ м | н ] _ к ;
        LBC  %>: _ [ #: | %>: [ Г1: | :Cns | %^О2:0 ] ] ; !! ало
         [ LBC - [ :* в ]  | :BTV ] %> %^О2: в _ л  ;
         #: Cns*  BTV [ [п|т|к|ц|ч] | м б | [в|с|ш] т | [р|л|н] д | [с|ш] к | [в|р|л|н] г | [т|д|р|л] м | в ш ] %>: _ [ #: | %>: [ %^О2:0 | с | т | н | ш | к | д | :в ] ] ; !! пандо


"O1:0"
О1:0 <=>   _  %>: [А2:а|А2:я] ;
          #: Cns* Vows Letters* :Vows в %> %^О2: в: _ л [ ь | и ] ;

"O2:ye"
%^О2:е <=>   LFCS %>: _ SIVV ;

"O2:e"
%^О2:э <=>  LFCH %>: _ SIVV ;
!         [ LFCH | FT %>: ( %^О2: ) з  ] %>: _ SIVV ;

"O2:o"
%^О2:о <=>
         LBC  %>: _ SIVV ;
         #:  Letters* BTV л ь [ ф | т | д ] %>: _ SIVV ;
! [ LBC | :BTV ( %>: ) з ] %>: _ [ ( %>: )  WFE | т %>: с | %>: :Cns ] ;

"O2:0"
%^О2:0 <=>   :Vows %>: _ SIVV ;

"A2:a"
А2:а <=>  [ LBC | :BTV ] %>: т  _  [ д | н ] ;
! [[ :BTV | LBC ] %>: т | %>: ( %^О2: ) м с %>: т ] _  [ д | н ] ;
!2013-03-16! [[ :BTV | LBC ] %>: [ т | TLoss:0 ( %>: ) т:0 ] | %>: ( %^О2: ) м с %>: т ] _  [ д | н ] ;
         [CnsDent ( э:0 | ы:0 | а:0 | О1: | о:0 | у:0 )|  в: т О1: м  О1:0 | CnsNonDent ( Vows:0 ) | [ LBC ( :BTV ) %>:  |  %^О2:о ]  ((н) т Ь2:0 | н Ь2: )  ] %>:  _  [ т | н ] ;



"j:0"
й:0 <=> Vows _ %>: [  Ы2:и | А2:я | %^О2: | О1: ] ;

"Е3:э always" !! %>: т н _ 2013-02-23
Е3:э <=     [ CnsDent ]  %>:  т н _ [ #: | %- | %>: [ Г1:0 а:я |т| л|Г2:в а | в | д | к | н | с | ш ]];

"Е3:э sometimes" !! %>: т н _ 2013-02-23
Е3:э =>     [ CnsDent | BTV Cns* CnsNonDent ]  %>:  т н _ [ #: | %- | %>: [ Г1:0 а:я |т| л|Г2:в а | в | д | к | н | с | ш ]];

"ye:e always"
е:э <=>
          CnsDent (О1:0) %>:  :н _ [[  #: | %-| %>: Г1:0 а:я]| н ь: | к] ;

! вазнэ, верьгизнэ, ривезьке
"Н1:н"
Н1:н <=> [ в | д | й | л | м | н | р ] ( ь: ) %> _ е:  ;

"Н1:к"
Н1:к <=> [ б | г | к | п | с | т | ф | х | ц | ч | ш | щ ] ( ь: ) %> _ е:  ;

"a:ya"
а:я  <=> :Vows ((%>:) К3:0 )  %>:  Г1:0 _  ;


!Changes in suffix consonants
"v:0"
в:0 <=> [ :Cns  ( ь: ) ] %>: _ т [О1:о|О1:е|О1:э] м [О1:о|О1:е|О1:0] ;
        #: Cns* Vows Letters* :Vows в %>: %^О2: _ О1: л  ;

"G1:0"
Г1:0 <=> [ :Vows ((%>:) К3:0 )  %>: | К1: Ь2:0 %>: KLoss:0 | :к %>: KLoss:0 ]  _ а:я ;

"G1:g"
Г1:г <=> [CnsVoicedDent (:ь|Ь2:0)| й | [ CnsVoicedNonDent - г ] ( :ь )] %>:  _ а ;

"G1:k"
Г1:к <=>  [ CnsVoicelessDent ( :ь | Ь2:0 | Ь2:ь ) | CnsVoicelessNonDent | г | К1: Ь2: | К4:0 ]  %>: _ а  ;
          

!Prolative
"G2:g"
Г2:г <=> [ CnsVoicedDent ( :ь )| [ CnsVoicedNonDent - г ] ( :ь ) ] %>: _ а ;

"G2:v"! 
Г2:в <=> [ :Vows (:0) %>: | :Vows ] _ а [ #: | %- |%>: [ Г1:0 |  %^О2:  SIVV |т | н | м | с | д: | в | к с | л ] ];


"G2:k"
Г2:к <=>  [ CnsVoicelessDent ( :ь )| CnsVoicelessNonDent ( :ь )| г ] %>: _ а [ #: |  %- |%>: [ Г1:0 | %^О2: SIVV | т | н | с | д: | в | к с | л ] ] ;
           Vows CnsVoiceless  %>: VLoss:0 _ а ;

! 2012-08-07
"G4:g" !Imperative Clitics
Г4:г  <=> з ь %>: _ а ;

"G4:0"
Г4:0  <=>
           [ К1: Ь2: | к ] %>: _ а ;
  [О1:е|О1:о] %>: _ а: ;

"G4:k"
Г4:к  <=>  #: [и л я|у ж о|а з ё|а д я|а й д а] %>: _ а  ;


!! imperative suffix
"K1:t"
К1:т <=>   Cns ( ь: )  %>: _ Ь2: ;



! Suffix final hard vs soft
"U4:y"
Ь2:ь <=>  [[[ LFCS | :FTV | ь: ] %>: | %^О2:е | %^О2:э | %^О2:0 | О1:е | О1:э ] [ н | ( н | в ) т ] |  [ LFCS | ь: | :FTV ] %>: К1:т ]  _  [ #: | %- | %>:  Г1: ] ;

!!SLoss s s > s = s 0 0 s
"s:0"
с:0 <=> с _ %>: SLoss:0 с ;

!Ablative d/t
"d:t"
д:т  <=> [ CnsVoiceless | д | г] (ь:0)  %>: _ [О1:е|О1:э|О1:о] ;


!Word final Vowels

"y:0"
ь:0 <=>    CnsDent  _  %>: [ А2:я | Ы2:и | %^О2:е | О1:е ];
              CnsDentNoL _ %>: [в:0 т О1:е м [О1:е|О1:0] | К1: | Н1:н | н | д: | ц е: [ #: | %- | %>: ] | т | с (т) [ ь: | е: ]] ;





"Remove morpheme boundary"
%>:0 <=> _ ;
