DELIMITERS = "<.>" "<..>" "<...>" "<!>" "<?>" "<Â¶>" sent ;
SOFT-DELIMITERS = "<,>" ;
SUBREADINGS = LTR ;

LIST BOS = (>>>) ;
LIST EOS = (<<<) ;

SETS

LIST N = n ;
LIST Np = np ;
LIST Prn = prn ;
LIST Det = det ;
LIST Have = vbhaver ;
LIST Be = vbser ;
LIST Adj = adj ;
LIST Predet = predet ;
LIST Adv = adv ;
LIST Pr = pr ;
LIST Ger = ger ;
LIST Num = num ;
LIST Rel = (prn rel) ;
LIST Ij = ij ;
LIST V = vblex vbser vaux vbmod vbhaver vbdo ;
LIST Vblex = vblex ;
LIST Vbser = vbser ;
LIST Vbhaver = vbhaver ;
LIST Vbdo = vbdo ;
LIST Vbaux = vaux ;
LIST Vbmod = vbmod ;
LIST Gen = gen ;
LIST Apos = apos ;
LIST Not = "not" ;
LIST Please = ("please" adv) ;
SET Do = ("do") ;
SET Imp = (vblex imp) | (vbser imp) | (vbdo imp) ;
SET Inf = (vblex inf) | (vbmod inf) | (vaux inf) | (vbser inf) | (vbhaver inf) | (vbdo inf) ;
SET Pp = (vblex pp) | (vbmod pp) | (vaux pp) | (vbser pp) | (vbhaver pp) | (vbdo pp) ;

LIST Sg = sg ;
LIST Pl = pl ;
LIST Sp = sp ;
LIST M = m ;
LIST F = f ;
LIST Nt = nt ;
LIST Mf = mf ;
LIST The = "the" ; 
LIST Fin = pres past imp ;
LIST To = "to" ;
SET Pers = (p1) | (p2) | (p3) ;

LIST P1 = p1 ; 
LIST P2 = p2 ; 
LIST P3 = p3 ; 

LIST PrnSubjP1Sg = (prn subj p1 mf sg) ;
LIST PrnSubjP1Pl = (prn subj p1 mf pl) ;
LIST PrnSubjP2Sp = (prn subj p2 mf sp) ;

LIST Letters = "a" "b" "c" "d" "e" "f" "g" "h" "i" "j" "k" "l" "m" "n" "o" "p" "q" "r" "s" "t" "u" "v" "w" "x" "y" "z" ;

LIST Ing = "<.*ing>"ri ;

SECTION

REMOVE Fin IF (-1C Det) ;

REMOVE Pl IF (0 Letters) ;

REMOVE (vblex) IF (-1 (gen)) ;

REMOVE (preadv) IF (1 (cm)) ;

#Lemmas that are N or V: N if DET/ADJ/PR precedes it
SELECT N IF (0 N) (0 V) (-1 Pr) ;
SELECT N IF (0 N) (0 V) (-1 Det) ;
SELECT N IF (0 N) (0 V) (-1 Adj) ;

#Lemmas that are N or V: V if ADV precedes it or ADV/N/PRN follows
SELECT V IF (0 N) (0 V) (-2 N) (-1 Adv) ;
SELECT V IF (0 N) (0 V) (-2 Det) (-1 N) ;
SELECT V IF (0 N) (0 V) (-1 Prn) ;
SELECT V IF (0 N) (0 V) (-1 Vbmod | Vbaux) ;
SELECT V IF (0 N) (0 V) (-1 Vbdo) ;
SELECT V IF (0 N) (0 V) (-2 Vbdo) (-1 Adv) ;
SELECT V IF (0 N) (0 V) (-3 Vbdo) (-2 Adv) (-1 Adv) ;

#Lemmas that are N or V: will/can/may
SELECT Vbmod IF (0 Vbmod) (0 N) (1 Inf) ;
SELECT Vbmod IF (0 Vbmod) (0 N) (1 Adv) (2 Inf) ;
SELECT Vbmod IF (0 Vbmod) (0 N) (1 Adv) (2 Adv) (3 Inf) ;

#Otherwise, select N
SELECT N IF (0 N) (0 V) ;

#Lemmas that are N or ADJ: N if ADJ precedes it or V follows
SELECT N IF (0 N) (0 Adj) (-1 Adj) ;
SELECT N IF (0 N) (0 Adj) (1 V) ;

#Lemmas that are N or ADJ: ADJ if N follows it
SELECT Adj IF (0 N) (0 Adj) (1 N) ;

# one : eins (num) / man (prn)
SELECT (num) IF (1 (n)) ;

REMOVE Inf IF (-1 BOS) ;
REMOVE (vblex pres) IF (-1 BOS) ;
REMOVE (vblex pp) IF (-1 BOS) ;
REMOVE (cnjadv) IF (-1 BOS) ;
REMOVE (preadv) IF (1 EOS) ;
REMOVE (vblex pp) IF (-1 (np)) ;
REMOVE (vblex pp) IF (-1 (prn subj)) ;
REMOVE (vbmod inf) IF (1 Inf) ;
REMOVE (vbmod inf) IF (1 Adv) (2 Inf) ;
REMOVE ("second"i n) IF (1 (n)) ;
#REMOVE (prn obj) IF (-1 V) (-2 BOS) ;
REMOVE (prn obj) IF (1 (vblex inf)) ;

SELECT (adv itg) IF (-1 BOS) ;
SELECT V IF (-1 (prn pers)) ;

SELECT SUB:1 Be IF (1 Ing) (0/* Have) ;

SELECT Ger IF (-1/* Be) (1 To) ;

SELECT Predet IF (1C The) ;

# Please (adv) and please (vblex)
SELECT Adv IF (0 Adv) (0 Vblex) (1 V) ;
SELECT Adv IF (0 Adv) (0 Vblex) (-1 _S_SOFT_DELIMITERS_) (1 _S_SOFT_DELIMITERS_) ;
SELECT Adv IF (0 Adv) (0 Vblex) (-1 BOS) (1 _S_SOFT_DELIMITERS_) ;
SELECT Adv IF (0 Adv) (0 Vblex) (-1 _S_SOFT_DELIMITERS_) (1 EOS) ;

# Saxon genitive ('s)
REMOVE Gen IF (NOT -1 N | Np) ;
REMOVE Gen IF (0 Gen) (0 Apos) (NOT -1 ("<.*s>"r)) ;
SELECT Gen IF (0 Gen) (0 Vbser) (0 Vbhaver) (-1 N + Pl) ;

# Have (vblex) and have (vbhaver)
REMOVE Vblex IF (0 Vblex) (0 Vbhaver) (1 Pp) ;
REMOVE Vblex IF (0 Vblex) (0 Vbhaver) (1 Adv) (2 Pp) ;
REMOVE Vblex IF (0 Vblex) (0 Vbhaver) (1 Adv) (2 Adv) (3 Pp) ;

# Verbs preceded by to
REMOVE Fin IF (-1C To) ;

# Do (vbdo) and do (vblex)
SELECT Vbdo IF (1 Inf) ;
SELECT Vbdo IF (1 Adv) (2 Inf) ;
SELECT Vbdo IF (1 Adv) (2 Adv) (3 Inf) ;
REMOVE Vbdo ;

# Remove imperatives unless preceded by "please" or placed at BOS
SELECT Imp IF (-1 BOS | _S_SOFT_DELIMITERS_) ;
SELECT Imp IF (-2 BOS) (-1 Please);
REMOVE Imp ;


# Verbs with person/number: P1
SELECT P1 IF (0 V) (-1 PrnSubjP1Sg | PrnSubjP1Pl)  ;
SELECT P1 IF (0 V) (-1 Adv) (-2 PrnSubjP1Sg | PrnSubjP1Pl) ;
SELECT P1 IF (0 V) (-1 Adv) (-2 Adv) (-3 PrnSubjP1Sg | PrnSubjP1Pl) ;
SELECT Sg IF (0 V) (-1 PrnSubjP1Sg) ;
SELECT Sg IF (0 V) (-1 Adv) (-2 PrnSubjP1Sg) ;
SELECT Sg IF (0 V) (-1 Adv) (-2 Adv) (-3 PrnSubjP1Sg) ;
SELECT Pl IF (0 V) (-1 PrnSubjP1Pl) ;
SELECT Pl IF (0 V) (-1 Adv) (-2 PrnSubjP1Pl) ;
SELECT Pl IF (0 V) (-1 Adv) (-2 Adv) (-3 PrnSubjP1Pl) ;

# Verbs with person/number: P2
SELECT P2 IF (0 V) (-1 PrnSubjP2Sp) ;
SELECT P2 IF (0 V) (-1 Adv) (-2 PrnSubjP2Sp) ;
SELECT P2 IF (0 V) (-1 Adv) (-2 Adv) (-3 PrnSubjP2Sp) ;

# Verbs with person/number: P3 (default) (if not genitive)
SELECT P3 IF (0 V) (NOT 0 ("'s")) ;

# a job / Steve Jobs
REMOVE Np IF (-1C (det ind)) ;
