# Firstly, we need to define what tags should be considered sentence delimiters. For this example, only full stop is set as delimiter.
DELIMITERS = "<.>" ;

SUBREADINGS = LTR ; 

LIST N = n ;
LIST Det = det ;
LIST Pr = pr ; 
LIST A = adj ;
LIST V = vblex ;

LIST Prop = np ;
LIST Mod = vbmod "vulè" ;
LIST Inf = inf ;
LIST Def = def ;

LIST Pos = pos ;

LIST Tonic = tn ; 
LIST Rel = rel ; 

LIST Enc = enc ; 
LIST Pro = pro ;

LIST Sg = sg ;

LIST Sg1 = (p1 sg) ;
LIST Sg3 = (p3 sg) ;

LIST Fin = pri prs pii pis fti ifi ; 

LIST Unk = "\\*.*"r ;

LIST Masc = m ; 
LIST Fem = f ;

LIST L' = "<l'>"i ;
LIST Surf/A = "<a>"i ; 
LIST DI = "<di>"i ; 
LIST SI = "<si>"i ; 
LIST UN = "<un>"i ;
LIST DÌ = "dì" ; 
LIST SÌ = "sì" ; 
LIST ÙN = "ùn" ; 
LIST Di = "di" ; 
LIST Vene = "vene" ; 

LIST IN = "in" ; 

LIST Lingue = "francesu" "francese" "danese" "inglese" "inglesu" "talianu" ; 

LIST Inf/à = ("<.*[Àà]>"r inf) ;
LIST Form/a = ("<.*[Aa]>"r inf) ;

SET Gen = Masc | Fem ;

SECTION

SELECT Pr IF (0 Surf/A) (1 Surf/A) ;

REMOVE DÌ IF (0 DI) ;

REMOVE ÙN IF (0 UN) ;

REMOVE SÌ IF (0 SI) ;

SELECT Inf IF (-1C Pr) ;

REMOVE Inf IF (NOT 0 Inf/à) (0 Form/a) ;

SELECT Enc IF (-1 Inf) (0 Pro OR Enc) ;

SELECT Enc IF (-2 Inf) (-1 Enc) (0 Pro OR Enc) ;

REMOVE Enc IF (NOT -1 Inf OR Enc) ;

SELECT Det IF (0 Surf/A) (1C Pos) ;

SELECT Pro IF (0 L') (1C Fin) ;

REMOVE Pr IF (0 Det OR Pr) (-1 Pr) (NOT 1 Det) ;

SELECT Sg IF (-1 IN) (0 Lingue) ;

SELECT Inf IF (0 Fin OR Inf) (-1 Mod) ;

SELECT Fin IF (NOT -1 Mod) (0 Vene) (1 Di LINK 1 Inf) ;

REMOVE Fin IF (-1 Fin) (0/* Enc) ;

REMOVE Sg1 IF (0C Sg1 OR Sg3) (-1 SI) ;

SELECT Fin IF (-2 Rel) (-1 Tonic) (0C Fin OR Inf) ;

SELECT Fin IF (-1 Rel) (0C Fin OR Inf) ;

SELECT Fin IF (-1C SI) (0C Fin OR Inf) ;

SELECT Fin IF (-2 Pro) (-1 Pro) (0C Fin OR Inf) ;

SELECT Fin IF (-2 Prop) (-1 Prop OR Unk) (0C Fin OR Inf) ;

REMOVE Pos IF (-1C Pro) ;

REMOVE Pos IF (-1 ÙN) ;

SELECT SUB:1 Def + $$Gen IF (0 Pr) (1C N + $$Gen) ;

SELECT N IF (-1C A) (0C V OR N) (1 Fin) ;

SELECT Fin IF (-1C Tonic) (0C Fin OR Inf) ;
