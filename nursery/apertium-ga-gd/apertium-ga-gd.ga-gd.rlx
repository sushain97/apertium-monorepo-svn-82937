DELIMITERS = "<.>" "<!>" "<?>" (sent); # end-of-sentence markers

LIST Verb = vblex;
LIST Vcop = (vbcop);

LIST Imp = imp;

LIST Conj = cnjcoo cnjsub;
LIST Adv = adv;
LIST Adj = adj;
LIST Prn = prn;

LIST DetDef = (det def);
LIST DetPos = (det pos);

LIST Nom = (n);
LIST NomF = (n f);
LIST NomM = (n m);

LIST NomProp = (np);
LIST Prep = (pr);

LIST Interr = (itg);
LIST Interj = (ij);

LIST hPro = ("<h.*>"r "[aeiouáéíóú].*"r) ("<h.*>"r "[AEIOUÁÉÍÓÚ].*"r);
LIST tPro = ("<t-.*>"r "[aeiouáéíóú].*"r) ("<t.*>"r "[AEIOUÁÉÍÓÚ].*"r);

# won't be there when the lemma begins with "h", and it is added when the "h" is added as an initial mutation
LIST INITIAL-H = ("<[Hh].*>"ri "[ABCDEFGIJKLMNOPQRSTUVWXYZÁÉÍÓÚabcdefgijklmnopqrstuvwxyzáéíóú].*"ri);

LIST Pronominal = (pr +prpers prn obj);

LIST RelDirect = (rel);

SET VerbImp = Verb + Imp;

# Lists from macra-ga.meta.pl 

LIST CAPITALLENITED = ("<[BCDFGMPST][Hh].*>");
LIST CAPITAL = ("<[A-Z]ÁÉÍÓÚ.*>");
LIST NMFOURSUBJ = (n m sg) (vblex prs);
LIST ECLIPSED = ("<d[Tt].*>"ri "[Tt].*"ri) ("<g[Cc].*>"ri "[Cc].*"ri) ("<b[Pp].*>"ri "[Pp].*"ri) ("<n[DdGg].*>"ri "[DdGg].*"ri)
                ("<bh[fF].*>"ri "[fF].*"ri) ("<n(?:-[aeiouáéíóú]|[AEIOUÁÉÍÓÚ]).*>"ri "[aeiouáéíóúAEIOUÁÉÍÓÚ].*"ri);
LIST UNECLIPSED = ("<[Tt].*>"ri "[Tt].*"ri) ("<[Cc].*>"ri "[Cc].*"ri) ("<[Pp].*>"ri "[Pp].*"ri) ("<[DdGg].*>"ri "[DdGg].*"ri)
                ("<[fF].*>"ri "[fF].*"ri) ("<(?:[aeiouáéíóú]|[AEIOUÁÉÍÓÚ]).*>"ri "[aeiouáéíóúAEIOUÁÉÍÓÚ].*"ri);

LIST LENITED  = ("<[bcdfgmpst]h.*>"r "[bcdfgmpst][^h].*"r);
LIST ANYCOPULA = (vbcop);
LIST PRSO = (prn) (pr) (adv);
LIST FUSEDPREP = ("<[Dd][eo]n|[Ss]an?|[Ff]aoin|[Óó]n>"ri);
LIST FUSEDPOSS = ("<([Dd]ár?|(?:[Ff]aoi|[Ii]|[Ll]e|[Tt]rí)n(?:a|ár))>"ri);
LIST UNLENITED = ("<[BbCcDdFfGgMmPpTt][^Hh']|[Ss][lnraeiouáéíóú]|bh[Ff]>"ri);

LIST GENITIVEPREP = ("<([Cc]ois|[Dd]ála|[Ff]earacht|[Tt]impeall|[Tt]rasna)>"ri pr);
LIST DATIVEPREP = ("<([Aa][grs]|[Cc]huig|[Dd][eo]|[Ff]aoi|[Gg]an|[Gg]o|[Ll]e|[Óó]|[Ii]n?|[Rr]oimh|[Tt]har|[Tt]ríd?|[Uu]m)>"ri pr);

##
## Here come the rules imported from An Gramadóir
##

SECTION

SELECT (n f sg) IF (-1 ("i"ri pr)) (0 ("<bhfuil>"));
SELECT (vblex pres) IF (0 ("<bhfuil>"));

SELECT (pr) IF (0 ("<thar>"ri));

SELECT (adv) IF (0 ("<chan>"ri)) (1 Verb); # 'U' → 'adv'

SELECT (pr) IF (0 ("<faoin>"ri));

SELECT (adv) IF (0 ("<thíos>"ri));
SELECT (adv) IF (0 ("<fiú>"ri));

SELECT (vblex prs) IF (-1 ("go"ri)) (0 NMFOURSUBJ) (0 ECLIPSED);

SELECT (adj sg nom) IF (-1 ("<nár>"ri)) (0 ("<chuma>"ri));

SELECT (vblex prs) IF (-1 ("<nár>"ri)) (0 (vblex prs)) (0 LENITED);
REMOVE (vblex prs) IF (0 (vblex prs)) (0 LENITED);

SELECT (n m sg nom) IF (0 ("<g?[Cc]h?éad>"ri)) (1 ("<seo>"));
SELECT (adj sg nom) IF (0 ("<g?[Cc]h?éad>"ri))

SELECT (adv) IF (0 ("<cheana>"ri));

SELECT Vcop IF (0 ("<más>"ri));

SELECT Vcop IF (0 ("<ab>"ri));

SELECT (n sg nom) IF (0 ("<áil>"ri));

SELECT (adj f sg gen) IF (0 ("<mh?óide>"ri));

#  most of these are the ones REQUIRING a preceding copula so this is important
#  18 Jan '05 added more needed to disambiguate preceding "ar" as copula
#  and not a preposition so we don't get "lenition missing" errors:
# "mian", "gá", "mó", "fiu", "deimhin", "suim", "fearr", "maith", "deacair"
# "fuath", "cóir", "cuma"
# only add these unlenited since this disambiguation is not always correct

SELECT Vcop IF (0 Vcop) (1 ("<(áil|ch?uimhin|eol|fh?éidir|oth)>"r n sg nom));
SELECT Vcop IF (0 Vcop) (1 ("<(háil|heol)>"r n sg nom)) (1 INITIAL-H);
SELECT Vcop IF (0 Vcop) (1 ("<(aithnid|mian|suim)>"r n f sg nom));
SELECT Vcop IF (0 Vcop) (1 ("<fuath>" n m sg nom));
SELECT Vcop IF (0 Vcop) (1 ("<gá>" n));
SELECT Vcop IF (0 Vcop) (1 ("<(dh?ócha|eagal|fh?oláir|ionann|leor|mh?iste)>"r adj sg nom)); 
SELECT Vcop IF (0 Vcop) (1 ("<(heagal|hionann)>"r adj sg nom)) (1 INITIAL-H);
SELECT Vcop IF (0 Vcop) (1 ("<(h?iomaí|léir|náir)>"r adj));
SELECT Vcop IF (0 Vcop) (1 ("<(cóir|cuma|deacair|dh?éanaí|deimhin|dh?ual|h?ea|éard|fiú|maith|mh?ithid)>"r));

SELECT (cnjcoo) IF (0 ("<Ach>"));

SELECT (adj) IF (0 ("<áirithe>"));

SELECT (ij) IF (0 ("<haló>"ri));

SELECT (n m sg nom) IF (0 ("<alt>"ri));
SELECT (n m sg gen) IF (0 ("<ama>"ri));

# <B>[Aa]s</B> <T>na</T>:<S>
REMOVE Pronominal IF (0 ("<as>"ri)) (1 ("<na>" det def));

# <B>[Aa]s</B> an:<S>
REMOVE Pronominal IF (0 ("<as>"ri)) (1 ("<an>"));

# <B>[Aa]s</B> <N>ANYTHING</N>:<S>
REMOVE Pronominal IF (0 ("<as>"ri)) (1 Nom);

SELECT Pronominal IF (0 ("<ann>"));

SELECT Vcop IF (0 ("<an>")) (1 ("<cúis>"ri n f sg nom));
SELECT:Vcop_if_Verb Vcop IF (0 ("<an>")) (1C Verb);        #  an fear scór
SELECT Vcop IF (0 ("<an>")) (1 ("<amhlaidh>"));
SELECT Vcop IF (0 ("<an>")) (1 ("<mar>"));

SELECT (det def) IF (0 ("<an>")) (1 ("té" prn));

SELECT Vcop IF (0 ("<an>")) (1 PRSO);
SELECT Vcop IF (0 ("<an>")) (1 ("<leis>"));
SELECT Vcop IF (0 ("<an>")) (1 ("<de>"));
SELECT Vcop IF (0 ("<an>")) (1 ("<faoi>"));

SELECT Vcop IF (1 ("<leis>") OR ("<de>") OR ("<faoi>"));

SELECT (det def) IF (0 ("<an>")) (1 ("<(chuir|mh?aith|ní)>"ri));

SELECT (det def) IF (0 ("<an>"ri)) (NOT 1 Verb);
SELECT (det def) IF (-1 (pr)) (0 ("<an>"));
SELECT (adv) IF (0 ("<anuas>"));

SELECT (n m sg nom) IF (-1 ("<mar>")) (0 ("<aon>"));
SELECT (adj sg nom) IF (0 ("<aon>"));

# "ag breathrú ar an ár"  (common form would have "t-")
SELECT (n m sg nom) IF (-1 ("<an>" det def)) (0 ("<ár>"));

SELECT (n m sg nom) IF (0 ("<ár>"ri)) (1 Conj);
SELECT (n m sg nom) IF (0 ("<ár>"ri)) (1 (det def));
SELECT (det pos) IF (0 ("<ár>"ri));

SELECT (vblex pres) IF (0 ("<atá>"ri));

# without "h" is handled by unigram
SELECT (n m sg nom) IF (0 ("<h[Aa]thair>"ri)) (1 INITIAL-H);

SELECT (n f pl nom) IF (-1 ("<na>"ri)) (0 ("<ba>"));
SELECT Vcop IF (0 ("<ba>"));
SELECT Vcop IF (-1 ("<([Gg]o|[Nn]ach)>")) (0 ("<mba>"ri));
SELECT Vcop IF (-1 ("<dh?á>"ri)) (0 ("<mba>"ri));

SELECT (vblex pres) IF (0 ("<bhaineann>"ri));

# catch barra here otherwise looks like nm1 gs/pl (really nm4 gs+pl of barr)
#  and gets disambiguated as the gs after "an"

SELECT (n m sg nom) IF (-1 ("<an>"ri det def)) (0 ("<barra>"ri));
SELECT (n m sg nom) IF (0 ("<bh?arr>"ri));
SELECT (adj nom pl) IF (0 ("<bh?eag>"ri));
SELECT (vblex past) IF (0 ("<bhí>"ri));

# verbal particle or copula
SELECT (vpart itg) IF (0 ("<cá>"ri)) (1 Verb);
SELECT Vcop IF (0 ("<cá>"ri)) (1 UNECLIPSED);

# friends, not credit
SELECT (n m pl nom) IF (0 ("<g?[Cc]h?airde>"));

SELECT (vpart itg) IF (0 ("<cár>"ri)) (1 Verb);
SELECT (vpart itg) IF (0 ("<cár>"ri)) (1 LENITED);

SELECT (n f sg gen) IF (-1 ("<na>" det def)) (0 ("<cé>"ri));
SELECT (vpart itg) IF (0 ("<cé>"ri));

SELECT (adj sg nom) IF (0 ("<ch?éanna>"ri));

# "dhó" is an exception to next, sometimes means "burning"
SELECT (n m sg nom) IF (-1 ("<(a|á)>"ri)) (0 ("<dhó>"));

# "sean" is actually "NUMERICAL" by accident:
SELECT (n m sg nom) IF (0 ("<[Ss]h?ean>"ri));

SELECT (n m sg nom) IF (-1 ("<an>"ri)) (0 ("<[Ss]h?ean>"ri));
SELECT (n m sg gen) IF (-1 ("<an>"ri)) (0 ("<[Mm]híle>"ri));

# handles fichiú, etc. by brute force; ceathrú is feminine so get separate rules
SELECT (n m sg nom) IF (0 ("<[Mm]híle>"));

### <B><Z>NUMERICAL</Z>ANYTHING</B>:<A pl="n" gnt="n">

# now do bliana since numbers are easier to match...
SELECT (n f pl nom) IF (-1 ("<d?[Tt]h?rí>"ri)) (0 ("<[Bb]liana>"ri));
SELECT (n f pl nom) IF (-1 ("<g?[Cc]h?eithre>"ri adj sg nom)) (0 ("<[Bb]liana>"ri));
SELECT (n f pl nom) IF (-1 ("<g?[Cc]h?úig>"ri adj sg nom)) (0 ("<[Bb]liana>"ri));
SELECT (n f pl nom) IF (-1 ("<[Ss]h?é>"ri)) (0 ("<[Bb]liana>"ri));
SELECT (n f pl nom) IF (-1 ("<[Ss]h?eacht>"ri adj sg nom)) (0 ("<[Bb]liana>"ri));
SELECT (n f pl nom) IF (-1 ("<h?[Oo]cht>"ri adj sg nom)) (0 ("<[Bb]liana>"ri));
SELECT (n f pl nom) IF (-1 ("<[Nn]aoi>"ri adj sg nom)) (0 ("<[Bb]liana>"ri));
SELECT (n f pl nom) IF (-1 ("<n?[Dd]h?eich>"ri adj sg nom)) (0 ("<[Bb]liana>"ri));

SELECT (n f sg gen) IF (0 ("<[Bb]liana>"ri));

#   beware "An Cheathrú Rua", "an cheathrú dheireanach"
SELECT (adj sg nom) IF (-1 ("<an>"ri)) (0 ("<[Cc]heathrú>")) (1 Nom);
SELECT (adj sg nom) IF (-1 FUSEDPREP) (0 ("<[Cc]heathrú>"));
SELECT (adj sg nom) IF (0 ("<g?[Cc]h?eathrú>"ri)) (1 UNLENITED) (1 Nom);
SELECT (adj sg nom) IF (0 ("<g?[Cc]h?eathrú>"ri)) (1 hPro) (1 Nom);

# fixes "rith chun síl", "cuir chun siúil"
REMOVE Verb IF (-1 ("<([Cc]huig|[Cc]hun)>"ri)) (NOT 0 Vcop);

# foilsitheoir, not "kick"
SELECT NomProp IF (0 ("<CIC>"));

SELECT (n m sg gen) IF (-1 ("<[Cc]hinn>"ri)) (0 (n f sg gen)) (0 ("<[Bb]liana>"ri));
SELECT (n m pl nom) IF (-1 DetDef) (-1 ("<[Nn]a>"ri)) (0 ("<cinn>"));

# includes "sna"; tá cinn móra aige
SELECT (n m pl nom) IF ((-1 Verb) OR (-1 Prep)) (0 ("<cinn>"));
SELECT (n m sg gen) IF (0 ("<cinn>"));

SELECT NomProp IF (0 ("<Colm">));
SELECT (n m sg nom) IF (0 ("<[Cc]omhalta>"));
SELECT (n m sg nom) IF (0 ("<g?ch?ónaí>"ri));

# since if it gets nb2, will want lenition on cainte
SELECT (n m pl nom) IF (0 ("<[Cc]ora>"ri)) (1 (nom f sg gen)) (1 ("<[Cc]ainte>"ri));
SELECT (n m pl nom) IF (0 ("<[Cc]ora>"ri)) (1 ("<[Cc]rua>"ri));

# lenition => prob not imperative
# "de réir an chuir síos", etc.

SELECT (n m sg gen) IF (-1 ("<an>"ri)) (0 ("<[Cc]huir>"ri));
SELECT (vblex past pl) IF (0 ("<[Cc]huir>"ri));
SELECT (vblex imp pl) IF (0 ("<[Cc]uir>"ri));
SELECT (n m pl gen) IF (-1 ("<na>"ri)) (0 ("<g[Cc]umann>"ri));
SELECT (n m sg nom) IF (0 ("<g[Cc]umann>"ri));

# idir an dá linn, Tadhg an dá thaobh
# OD dhá
SELECT (adj sg nom) IF (-1 ("<[Aa]o?n>"ri)) (0 ("<[Dd]á>"ri));

# OD dhá
SELECT (adj sg nom) IF (-1 ("<g?ch?éad>"ri)) (0 ("<dá>"));
SELECT (adj sg nom) IF (-1 ("<[Ss]a>"ri)) (0 ("<dá>"));

SELECT Conj IF (0 ("<[Dd]á>"ri)) (1 Verb);
SELECT DetPos IF (0 ("<[Dd]á>"ri)) (1 Nom);

# "dá hóige"
SELECT DetPos IF (0 ("<[Dd]á>"ri)) (1 hPro);

SELECT (n f sg nom) IF (-1 ("<an>"ri)) (-1 DetDef) (0 ("<dála>"));

#  names of the days of the week in comhshuite so these are "beannacht Dé"

SELECT (n m sg gen) IF (0 ("<Dé>"));

# noun forms in phrases "ó dheas", "i ndeas", etc. always mutated
# doing this here helps "go+<A>"

SELECT (adj sg nom) IF (0 ("<[Dd]eas>"));

# ok as is; "trí bliana déag", etc.  "trí déag"... in comhshuite
SELECT (n sg nom) IF (0 ("<n?[Dd]h?éag>"ri));
SELECT (vblex imp) IF (0 ("<n?[Dd]h?éan>"ri));
SELECT (vblex past) IF (0 ("<n?[Dd]h?earna>"ri));
SELECT (vblex pres) IF (0 ("<[Dd]eir>"ri));
SELECT (vblex pii) IF (-1 ("<[Aa]>"ri)) (0 ("<[Dd]eireadh>"ri));

SELECT RelDirect IF (0 ("<[Aa]>"ri)) (1 ("<[Dd]eireadh>"ri)) (1 (vblex pii pl));

SELECT (n m sg nom) IF (0 ("<[Dd]eireadh>"ri));
SELECT (n m sg gen) IF (0 ("<[Dd]h?eiridh>"ri));

SELECT Pronominal IF (0 ("<díobh>"));
SELECT Prep IF (0 ("<[Dd]o>"ri)) (1 DetDef) (1 ("<na>"));

SELECT DetPos IF (-1 Prep) (0 ("<do>"));
SELECT Prep IF (0 ("<do>")) (1 DetPos);
SELECT Prep IF (0 ("<do>")) (1 NomProp);
SELECT Prep IF (0 ("<do>")) (1 (adj sg nom)) (1 ("<gach>"));

# fad do shaoil
SELECT DetPos IF (0 ("<do>")) ((1 RelDirect) OR (1 Nom));

REMOVE Verb IF (-1 ("<[Dd]o>"ri)) (0 Verb);

SELECT (n sg nom) IF (-1 ("<a>")) (0 ("<dó>"));
SELECT (adj sg nom) IF (-1 DetDef) (-1 ("<an>")) (0 ("<dó>"));

# ag dó na geirbe
SELECT (n m sg nom) IF (-1 Prep) (-1 ("<[Aa]g>"ri)) (0 ("<[Dd]ó>"ri))

# dó gréine, ~ seaca, etc.
SELECT (n m sg nom) IF (0 ("<[Dd]ó>"ri)) ((1 Nom) OR (1 RelDirect));

# do it now or else get genitive noun from "tugann litir dó"
SELECT Pronominal IF (0 ("<[Dd]ó>"ri));

# imperative only other choice; this speeds up "dóigh a:<H>" below
SELECT (n f sg nom) IF (0 ("<dóigh>"));

# logainmneacha
SELECT (n m sg nom) IF (0 ("<Dh?un>"ri));
SELECT Prn IF (0 ("<[Éé]>"ri));

# obvious <S> cases here; last resort PREPPRONM rules at end
SELECT Prep IF (0 ("<[Ff]aoi>"ri)) (1 DetDef) (1 ("<na>"));
SELECT Prep IF (0 ("<[Ff]aoi>"ri)) (1 ("<an>"));
SELECT Prep IF (0 ("<[Ff]aoi>"ri)) (1 Nom);

SELECT (n m sg nom) IF (0 ("<[Ff]eadh>"ri));
SELECT (n m pl gen) IF (-1 ("<na>")) (-1 DetDef) (0 ("<bh[Ff]ear>"ri));

SELECT Adv IF (0 ("<[Ff]éin>"ri));

# grass, not gsm "fiar"
SELECT (n m sg gen) IF (0 ("<[Ff]h?éir>"ri));
SELECT (vblex past) IF (0 ("<(bh)?[Ff]uair>"ri));
SELECT NomProp IF (-1 CAPITAL) (0 ("<Gabhann>"));
SELECT (vblex pres) IF (0 ("<Gabhann>"));

SELECT (n m pl nom) IF (0 ("<g[Cc]inn>"ri));

#  avoids lenition error on "acmhainn grinn"
SELECT (n m sg gen) IF (-1 (n f sg nom)) (0 ("<grinn>"));

# see "gur" last resort...
SELECT (n f sg gen) IF (-1 DetDef) (-1 ("<na>")) (0 ("<huaire>")) (0 INITIAL-H);

# all others are like "ceithre huaire"
SELECT (n f pl nom) IF (0 ("<huaire>")) (0 INITIAL-H);

# compare "bheith" above
REMOVE (n gen) IF (-1 ("<iarraidh>" n f sg nom)) (0 (n gen));

#  not "Carry!" imperative
SELECT (n m sg gen) IF (0 ("<iompair>"));

# not "yam" :)
SELECT Pronominal IF (0 ("<[Ii]onam>"ri));

# amháin, siar, thiar, thuaidh, etc; iontach only prominent exception
SELECT (adj sg nom) IF (0 ("<iontach>"));
SELECT Adv IF ((0 Adv) OR (0 Adj)); 

# theas is also "heat" so not officially an ADJADV.  "heat" via unigram.
SELECT (adj sg nom) IF ((-1 Prep) OR (-1 DetDef)) (0 ("<theas>"));

SELECT (n m sg nom) IF (0 ("<[Ll]án>"ri)) ((1 Nom) OR (1 Prn));
SELECT (n m sg nom) IF (0 ("<[Ll]án>"ri)) (1 DetPos);
SELECT (adj sg nom) IF (0 ("<[Ll]án>"ri)) (1 Prep) (1 ("<de>"));

SELECT (adj sg nom) IF (-1 Prep) (-1 ("<[Ll]e>"ri)) (0 ("<(haon|hocht)>"ri)) (0 INITIAL-H);
SELECT (n f sg nom) IF (-1 Prep) (-1 ("<[Ll]e>"ri)) (0 (n f sg nom)) (0 INITIAL-H);
SELECT (n m sg nom) IF (-1 Prep) (-1 ("<[Ll]e>"ri)) (0 (n m sg nom)) (0 INITIAL-H);
SELECT (n f pl nom) IF (-1 Prep) (-1 ("<[Ll]e>"ri)) (0 (n f pl nom)) (0 INITIAL-H);
SELECT (n m pl nom) IF (-1 Prep) (-1 ("<[Ll]e>"ri)) (0 (n m pl nom)) (0 INITIAL-H);
SELECT (n m sg gen) IF (0 ("<[Ll]éinn>"ri));

SELECT Prep IF (0 ("<[Ll]eis>"ri)) (1 DetDef) (1 ("<[Nn]a>"ri));
SELECT Prep IF (0 ("<[Ll]eis>"ri)) (1 DetDef) (1 ("<[Aa]n>"ri));

SELECT (n f sg nom) IF (0 ("<[Ll]eith>"ri));
SELECT Pronominal IF (0 ("<[Ll]eo>"ri));

# <T> should be "na" but plenty of errors like "i nGaeilge an linne seo"
SELECT (n f sg gen) IF (-1 ("<a>")) (0 ("<[Ll]inne>"ri));
SELECT (n f sg gen) IF ((-1 DetDef) OR (-1 DetPos)) (0 ("<[Ll]inne>"ri));
SELECT Pronominal IF (0 ("<[Ll]inne>"ri));

SELECT Conj IF (0 ("<[Mm]á>"ri)) (1 Verb);

# esp. important to get "mhaith" right else "NISEIMHIU" adj errors
SELECT (n f sg nom) IF (-1 ("<[Óó]>"ri)) (0 ("<mhaith>"));
SELECT (n f sg nom) IF (-1 Prep) (0 ("<mh?aith>"));
SELECT (n f sg nom) IF (-1 Interr) (-1 ("<[Cc]én>"ri)) (0 ("<mh?aith>"));

# "an" resolved as <T> above (copula rules)
SELECT (n f sg nom) IF (-1 DetDef) (-1 ("<an>")) (0 ("<mh?aith>"));
SELECT (n f sg nom) IF (-1 (adj sg nom)) (-1 ("<([Aa]on|[Cc]héad)>"ri)) (0 ("<mh?aith>"));
SELECT (adj sg nom) IF (0 ("<[Mm]h?aith>"));

SELECT Prep IF (0 ("<[Mm]ar>"ri)) (1 ("<gheall>"));

SELECT (n m sg gen) IF (-1 DetDef) (-1 ("<an>")) (0 ("<mheasa>"));

# almost always "ba mheasa"
SELECT (adj f sg gen) IF (-1 Vcop) (0 ("<mh?easa>"ri));

# exceptions to default feminine tag couple lines down
SELECT (n m sg gen) IF (-2 GENITIVEPREP) (-1 DetDef) (-1 ("<[Aa]n>"ri)) (0 ("<mhéid>"));

SELECT (n f sg nom) IF (-1 DetDef) (-1 ("<[Aa]n>"ri)) (0 ("<mhéid>"));
SELECT (n m sg nom) IF (-1 Prep) (0 ("<mh?ór>"ri));
SELECT (n m sg nom) IF (-1 DetDef) (-1 ("<[Aa]n>"ri)) (0 ("<mh?ór>"ri));
SELECT (n m sg gen) IF (-1 DetDef) (-1 ("<[Aa]n>"ri)) (0 ("<mh?óir>"ri));

SELECT (adj m sg gen) IF (0 ("<[Mm]h?óir>"ri));
SELECT (adj sg nom) IF (0 ("<[Mm]h?ór>"ri));
SELECT (adj pl nom) IF (0 ("<[Mm]h?óra>"ri));
SELECT (adj sg nom) IF (-1 DetDef) (-1 ("<[Nn]a>"ri));

# note that next rule is disjoint from previous (so don't put <U> in next one)

SELECT (vblex imp)  IF (-1 ("<[Nn]á>"ri)) (0 VerbImp);

SELECT Conj IF (0 ("<[Nn]á>"ri));

# since "ná" verbs are handled in previous batch, now can do next two:
# e.g.  go hInis, á húsáid, á hól  are nouns...

SELECT (n m sg nom) IF (0 NomM) (0 VerbImp) (0 INITIAL-H);
SELECT (n f sg nom) IF (0 NomF) (0 VerbImp) (0 INITIAL-H);

SELECT (adj sg nom) IF (-1 Nom) (-1 ("<Spiorad>")) (0 ("<Naomh>"));

# Comaoin na Naomh
SELECT (n m pl gen) IF (-1 DetDef) (-1 ("<na>")) (0 ("<[Nn]aomh>"ri));
SELECT (n m sg nom) IF (0 ("<[Nn]aomh>"ri));

#   "an" not yet disambig?
SELECT (n m sg nom) IF (-1 DetDef) (-1 ("<[Aa]n>"ri)) (0 ("<[Nn]í>"ri));
SELECT (n m sg nom) IF (0 ("<[Nn]í>"ri)) (1 ("<nach>"));
SELECT (n m sg nom) IF (0 ("<[Nn]í>"ri)) (1 ("<a>"));

SELECT (n m sg nom) IF (-1 Adj) (-1 ("<([Aa]on|[Gg]ach)>"ri)) (0 ("<[Nn]í>"ri)) (1 ("<a>"));

# mór done above

SELECT Vcop IF (0 ("<[Nn]í>"ri)) (1 ("<mór>" adj sg nom));
SELECT Vcop IF (-1 Conj) (0 ("<ní>")) (NOT 1 Verb);
SELECT Vcop IF (0 ("<[Nn]í>"ri)) (1 Nom);
SELECT Vcop IF (0 ("<[Nn]í>"ri)) (1 Prn);

# River
SELECT (n f sg nom) IF (-1 ("<[Aa]n>"ri)) (0 ("<Níl>"));

SELECT (vblex pres pl) IF (0 ("<Níl>"));

SELECT Conj IF (0 ("<[Nn]uair>"ri));

# not "deer"
SELECT Prep IF (0 ("<os>"));

SELECT (n f sg gen) IF (-1 DetDef) (-1 ("<na>")) (0 ("<[Rr]inne>"ri));

SELECT (vblex pres) IF (0 ("<[Rr]inne>"ri));

SELECT (n sg nom) (0 ("<San>")) (1 CAPITAL);
SELECT Prep (0 ("<San>"));

SELECT (n m sg nom) IF (0 ("<[Ss]cór>"ri));

# gender problem?  - need this tag right or get "BACHOIR{í}" errors

SELECT (n m sg gen) IF (-1 (n f sg nom)) (-1 ("<m?bh?ean>"ri)) (0 ("<[Ss]í>"ri));
SELECT (n m sg gen) IF (-1 (n f pl gen)) (-1 ("<m?bh?an>"ri)) (0 ("<[Ss]í>"ri));
SELECT (n m sg gen) IF (-1 (n m sg nom)) (-1 ("<aos>"ri)) (0 ("<[Ss]í>"ri));
SELECT (n m sg gen) IF (0 ("<[Ss]í>"ri)) (1 (n f sg gen)) (1 ("<gaoithe>"));
SELECT (n m sg gen) IF (0 ("<[Ss]í>"ri)) (1 (n m sg gen)) (1 ("<deannaigh>"));
SELECT (n m sg gen) IF (-1 DetDef) (-1 ("<[Nn]a>"ri)) (0 ("<[Ss]í>"ri));

# capital Sí is sometimes the noun; need for sé below

SELECT Prn IF (0 ("<sí>"));
SELECT NomProp IF (-1 ("<Ó>")) (0 ("<Sé>"));

# Sé Chontae usually if caps; won't catch "beidh sé c_athair thearmainn"!
SELECT (adj sg nom) IF (0 ("<[Ss]é>"ri)) (1 LENITED);
SELECT (adj sg nom) IF (-1 ("<a>")) (0 ("<[Ss]é>"ri));

SELECT Prn IF (-2 Prn) (-2 ("<sí>")) (-1 Conj) (-1 ("<nó>")) (0 ("<sé>"));

# like cúig nó sé, buille an sé, na Sé Chontae, etc.
#  this is slow but gets the job done; other attempts don't
#  have the same accuracy

SELECT (adj sg nom) IF (NOT -1 Verb) (0 ("<[Ss]é>"));

SELECT Prn IF (0 ("<[Ss]é>"));

#  "they", not "a growth"!
SELECT Prn IF (0 ("<[Ss]iad>"));

SELECT Prn IF (-1 Prep) (0 Prn) (0 Adj);
SELECT Prn IF (-1 Interr) (0 Prn) (0 Adj);
SELECT Prn IF (-1 Verb) (0 Prn) (0 Adj);
SELECT Prn IF (-1 Conj) (0 Prn) (0 Adj);
SELECT Prn IF (-1 ("<[Aa]ch>"ri)) (0 Prn) (0 Adj);
SELECT Prn IF (-1 ("<[Aa][rs]>"ri)) (0 Prn) (0 Adj);
SELECT Prn IF (-1 ("<[Dd]e>"ri)) (0 Prn) (0 Adj);
SELECT Prn IF (-1 ("<[Ff]aoi>"ri)) (0 Prn) (0 Adj);

SELECT Prep IF (0 ("<[Dd]e>"ri)) (1 Prn) (1 ("<(seo|sin)>"ri));

SELECT Prn IF (-1 ("<[Gg]ur>"ri)) (0 Prn) (0 Adj);
SELECT Prn IF (-1 ("<[Mm]ar>"ri)) (0 Prn) (0 Adj);
SELECT Prn IF (-1 ("<[Óó]>"ri)) (0 Prn) (0 Adj);

SELECT Adj IF (0 Prn) (0 Adj);

# compare with similar le,na rules above
SELECT (adj sg nom) IF (-1 Prep) (-1 ("<[Ss]na>"ri)) (0 ("<(haon|hocht)>"ri)) (0 INITIAL-H);
SELECT (n f pl nom) IF (-1 Prep) (-1 ("<[Ss]na>"ri)) (0 (n f pl nom));
SELECT (n m pl nom) IF (-1 Prep) (-1 ("<[Ss]na>"ri)) (0 (n m pl nom));

# not imperative/past verb
SELECT (n m sg nom) IF (0 ("<[Ss]námh>"ri));

SELECT Prep IF (0 ("<[Tt]har>"ri));
SELECT Adv IF (0 ("<[Tt]hart>"ri));

# "east" not genitive of "bush".

SELECT (adj sg nom) IF (-1 Nom) (0 ("<[Tt]hoir>"));

SELECT Adv IF (0 ("<[Tt]hoir>"));

SELECT (n m sg nom) IF (0 ("<theannta>"));

# house, not golf
SELECT (n m sg gen) IF (0 ("<[Tt]h?í>"ri));
SELECT (n m sg nom) IF (-1 Prep) (-1 ("<[Aa]g>")) (0 ("<[Tt]rá>"ri));

REMOVE (n m) IF (0 ("<d?[Tt]h?rá>"ri));

SELECT NomProp IF (0 ("<Treasa>"));

# le trí chúilín, an trí chúilín
SELECT (adj sg nom) IF ((-1 Prep) OR (-1 DetDef)) (0 ("<[Tt]rí>"ri));
SELECT (adj sg nom) IF (0 ("<[Tt]rí>"ri)) (1 ("<(bliana|cinn|fichid|seachtaine)>"ri));

SELECT Prep IF (0 ("<[Tt]rí>"ri));
SELECT Prep IF (0 ("<[Tt]ríd>"ri)) (1 DetDef) (1 ("<na>"));
SELECT Prep IF (0 ("<[Tt]ríd>"ri)) (1 ("<an>"));
SELECT Pronominal IF (0 ("<[Tt]ríd>"ri));

SELECT NomProp IF (0 ("<Uí>"));

# Aire, aire; first one needs <S> resolved (leis, etc)
# but do it before the <S> ANYNMC rule below!

SELECT (n m sg nom) IF (-2 ("<[Aa]r>"ri)) (-1 DetDef) (-1 ("<[Aa]n>"ri)) (0 ("<Aire>"));
SELECT (n m sg gen) IF (-1 DetDef) (-1 ("<[Aa]n>"ri)) (0 ("<Aire>"));
SELECT (n f sg nom) IF (0 ("<(n-)?aire>"ri));

# agus bhog mé...   a cheannaithe féin...  not adjectives
#  do this after "the" rules, and NUMERICAL, etc.

REMOVE Adj IF (NOT -1 Adj) (NOT -1 Nom) (NOT -1 Verb) (0 Adj) (0 LENITED);

REMOVE (n gen) IF (-1 (Interj) OR (-1 Pronominal) OR (-1 Interr) OR (-1 Verb)) (0 (n gen));
REMOVE (n gen) IF (-1 DATIVEPREP) (0 (n gen));
REMOVE (n gen) IF (-1 Prep) (-1 ("<[Gg]o dtí>"ri)) (0 (n gen));
REMOVE (n gen) IF (-1 FUSEDPOSS) (0 (n gen));
REMOVE (n gen) IF (-2 DATIVEPREP) (-1 DetPos) (0 (n gen));
REMOVE (n gen) IF (NOT -2 Adj) (NOT -2 Conj) (NOT -2 Nom) (NOT -2 Adv) (NOT -2 Prep) (NOT -2 NomProp) (-1 DetPos) (0 (n gen));

##
## Rules added after Gramadóir
##

REMOVE Verb IF (-1C Vcop);
SELECT (vpart) IF (0 ("<an>"ri)) (NOT 1 Nom);
REMOVE (gen) IF (-1C Verb) ((0 (n gen)) OR (0 (det def gen)));
REMOVE (n gen) IF (-1C DetDef) (NOT -1 (det def gen)) (0 (n gen));
REMOVE (adj gen) IF (-1C Nom) (NOT -1 (n gen)) (0 (adj gen));
REMOVE (adj pl) IF (-1C Nom) (NOT -1 (n pl)) (0 (adj pl));
REMOVE (adj sg) IF (-1C Nom) (NOT -1 (n sg)) (0 (adj sg));

# i gcathair na Gaillimhe
SELECT:ArtGen_if_NPGen (det def sg gen) IF (1C (np gen)); 
